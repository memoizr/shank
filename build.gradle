plugins {
    id "me.tatarka.retrolambda" version "3.2.3"
}

retrolambda {
    jdk '/usr/lib/jvm/java-8-oracle'
    oldJdk '/usr/lib/jvm/java-7-oracle'
    javaVersion JavaVersion.VERSION_1_7
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'
group = 'com.github.memoizr'
version = 'v0.2.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
    classDirectories = fileTree(
            dir: './build/classes/main/com/memoizrlabs/',
            excludes: [
                    '**/Lambda$*.class',
                    '**/Lambda.class',
                    '**/*Lambda.class',
                    '**/*Lambda*.class'
            ]
    )
}

dependencies {
    compile 'com.github.memoizr:PowerOptional:v0.1.0'
    compile group: 'com.github.memoizr', name: 'functions', version: 'v0.4.0'
    testCompile "nl.jqno.equalsverifier:equalsverifier:2.1.5"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('X', '-quiet')
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}
